buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.6'
    }
}

apply plugin: 'com.bmuschko.docker-remote-api'

docker {
    url = System.env.DOCKER_HOST ?: 'https://192.168.99.100:2376'
    certPath = file("${System.getProperty('user.home')}/.docker/machine/machines/default")

    registryCredentials {
        url = 'qaware-oss-docker-registry.bintray.io'
        username = 'qaware-oss'
        password = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
        email = 'qaware-oss@qaware.de'
    }
}

repositories {
    jcenter()
}

version = '8u77'

import com.bmuschko.gradle.docker.tasks.DockerInfo
import com.bmuschko.gradle.docker.tasks.DockerVersion
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task buildDockerImage(type: DockerBuildImage) {
    inputDir = projectDir
    noCache = false
    remove = true
    tag = "$project.name:$version"
}

task removeDockerImage(type: DockerRemoveImage) {
    imageId = "$project.name:$version"
}

task pushDockerImage(type: DockerPushImage) {
    imageName = "qaware-oss-docker-registry.bintray.io/base/$project.name:$version"
}

task pushDockerLatest(type: DockerPushImage) {
    imageName = "qaware-oss-docker-registry.bintray.io/base/$project.name"
}

task pushDockerImages() {
    dependsOn pushDockerImage, pushDockerLatest
}

task dockerInfo(type: DockerInfo) {}
task dockerVersion(type: DockerVersion) {}
